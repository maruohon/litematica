plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    maven { url 'https://masa.dy.fi/maven' }
    // maven { url 'https://maven.terraformersmc.com/releases/' }
    maven { url 'https://maven.quiltmc.org/repository/release/' }
}

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

dependencies {
    minecraft "com.mojang:minecraft:${config.minecraft_version}"
    mappings "net.fabricmc:yarn:${config.mappings_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${config.fabric_loader_version}"
    implementation "com.google.code.findbugs:jsr305:3+"

    modImplementation "fi.dy.masa.malilib:malilib-fabric-${config.minecraft_version_out}:${config.malilib_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    //modCompile "net.fabricmc:fabric:" + config.fabric_version

    modCompileOnly "com.terraformersmc:modmenu:${config.mod_menu_version}"
}

minecraft {
    ext.mod_version = config.mod_version

    if (mod_version.endsWith('-dev')) {
        mod_version = mod_version + "." + new Date().format('yyyyMMdd.HHmmss')
    }
}

group = config.group + "." + config.mod_id
archivesBaseName = config.mod_file_name + '-' + config.minecraft_version_out
version = project.minecraft.mod_version

processResources {
    // Exclude the GIMP image files
    exclude '**/*.xcf'
    exclude '**/xcf'

    // this will ensure that this task is redone when the versions change.
    //inputs.property "minecraft_version", project.config.minecraft_version

    inputs.property "mod_version", project.minecraft.mod_version

    filesMatching("fabric.mod.json") {
        expand "mod_version": project.minecraft.mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Minecraft 1.17 (21w19a) upwards uses Java 16.
    it.options.release = 16
}
